buildscript {
  repositories {
    mavenCentral()
    gradlePluginPortal()
    maven { url 'https://repo.spring.io/plugins-release' }
  }
}

plugins {
  id 'idea'
  id 'eclipse'
  id 'java'
}

subprojects {

  group = 'com.vmware.gemfire'

  sourceCompatibility = '17'
  targetCompatibility = '17'

  repositories {
    mavenCentral()
    maven {
      credentials {
        username "$gemfireRepoUsername"
        password "$gemfireRepoPassword"
      }
      url = uri("https://commercial-repo.pivotal.io/data3/gemfire-release-repo/gemfire")
    }

    def additionalMavenRepoURLs = project.ext.get('additionalMavenRepoURLs')
    if (!additionalMavenRepoURLs.isEmpty() && !additionalMavenRepoURLs.isBlank()) {
      additionalMavenRepoURLs.split(",").each {
        project.getRepositories()
                .maven(mavenRepository -> {
                  mavenRepository.setUrl(uri(it.toString()));
                });
      }
    }
    maven { url 'https://repo.spring.io/milestone' }
  }

  configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, "minutes"
  }
}

String getGemFireBaseVersion() {
  def split = "${gemfireVersion}".split("\\.")
  if (split.length < 2) {
    throw new RuntimeException("gemfireVersion is malformed")
  }
  return "${split[0]}.${split[1]}"
}

