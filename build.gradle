buildscript {
  repositories {
    mavenCentral()
    gradlePluginPortal()
    maven { url 'https://repo.spring.io/plugins-release' }
  }
}

plugins {
  id 'org.springframework.boot' version "${springBootVersion}" apply false
  id 'idea'
  id 'eclipse'
  id 'java'
}

allprojects {

  group = 'com.vmware.gemfire'

  sourceCompatibility = '1.8'
  targetCompatibility = '1.8'

  repositories {
    mavenLocal()
    mavenCentral()
    maven {
      credentials {
        username "$gemfireRepoUsername"
        password "$gemfireRepoPassword"
      }
      url = uri("https://commercial-repo.pivotal.io/data3/gemfire-release-repo/gemfire")
    }
  }

  repositories {
    def additionalMavenRepoURLs = project.ext.get('additionalMavenRepoURLs')
    if (!additionalMavenRepoURLs.isEmpty() && !additionalMavenRepoURLs.isBlank()) {
      additionalMavenRepoURLs.split(",").each {
        project.getRepositories()
                .maven(mavenRepository -> {
                  mavenRepository.setUrl(uri(it.toString()));
                });
      }
    }
  }

  configurations.all {
    resolutionStrategy.cacheChangingModulesFor 0, "minutes"
  }
}

String getGemFireBaseVersion() {
  def split = "${gemfireVersion}".split("\\.")
  if (split.length < 2) {
    throw new RuntimeException("gemfireVersion is malformed")
  }
  return "${split[0]}.${split[1]}"
}

def exportedProjects= [
        ":spring-gemfire",
        ":spring-gemfire-actuator",
        ":spring-gemfire-actuator-autoconfigure",
        ":spring-gemfire-autoconfigure",
        ":spring-gemfire-extensions",
        ":spring-gemfire-starter",
        ":spring-gemfire-starter-logging",
        ":spring-gemfire-starter-session",
        ":spring-gemfire-starter-test",
]

tasks.register('combinedJavadoc', Javadoc)  {
  source exportedProjects.collect { project(it).sourceSets.main.allJava }
  title = "Spring Boot for VMware GemFire Java API Reference"
  classpath = files(exportedProjects.collect {
    project(it).sourceSets.main.compileClasspath
  })
  destinationDir = file("${buildDir}/docs/javadoc")
}

tasks.register('combinedJavadocJar', Jar) {
  dependsOn combinedJavadoc
  archiveClassifier = 'javadoc'
  from combinedJavadoc.destinationDir
}